#!groovy

@Library("Infrastructure")

import uk.gov.hmcts.contino.AppPipelineDsl
import uk.gov.hmcts.contino.GithubAPI
import uk.gov.hmcts.contino.GradleBuilder

def type = "java"
def product = "pcs"
def component = "api"

def secrets = [
  'pcs-${env}': [
    secret('pcs-api-s2s-secret', 'PCS_API_S2S_SECRET'),
    secret('idam-system-user-name', 'IDAM_SYSTEM_USERNAME'),
    secret('idam-system-user-password', 'IDAM_SYSTEM_USER_PASSWORD'),
    secret('pcs-api-idam-secret', 'PCS_API_IDAM_SECRET'),
    secret('idam-pcs-user-password', 'IDAM_PCS_USER_PASSWORD'),
    secret('pcs-solicitor-automation-uid', 'PCS_SOLICITOR_AUTOMATION_UID'),
    secret('pcs-prd-admin-username', 'PCS_PRD_ADMIN_USERNAME'),
    secret('pcs-prd-admin-password', 'PCS_PRD_ADMIN_PASSWORD'),
    secret('idam-expired-user-token', 'IDAM_EXPIRED_USER_TOKEN'),
    secret('s2s-expired-service-token', 'S2S_EXPIRED_TOKEN'),
  ]
]

static LinkedHashMap<String, Object> secret(String secretName, String envVar) {
  [ $class: 'AzureKeyVaultSecret',
    secretType: 'Secret',
    name: secretName,
    version: '',
    envVariable: envVar
  ]
}

GradleBuilder builder = new GradleBuilder(this, product)

withPipeline(type, product, component) {
  def githubApi = new GithubAPI(this)
  env.IDAM_S2S_AUTH_URL = "http://rpe-service-auth-provider-aat.service.core-compute-aat.internal"
  env.IDAM_API_URL = "https://idam-api.aat.platform.hmcts.net"
  env.PACT_BROKER_URL = "https://pact-broker.platform.hmcts.net"
  loadVaultSecrets(secrets)
  enableDbMigration('pcs')

  onPR() {
    env.CASE_TYPE_SUFFIX="${CHANGE_ID}"
    enablePactAs([
      AppPipelineDsl.PactRoles.CONSUMER,
      AppPipelineDsl.PactRoles.CONSUMER_DEPLOY_CHECK
    ])
    setPreviewEnvVars()
    if (githubApi.getLabelsbyPattern(env.BRANCH_NAME, "pr-values:ccd").size() > 0) {
      enableHighLevelDataSetup()
    }
    if (githubApi.getLabelsbyPattern(env.BRANCH_NAME, "enable_e2e_full_regression").size() > 0) {
      env.E2E_SUITE = "chrome"
      enableE2eTest()
    }
    else if (githubApi.getLabelsbyPattern(env.BRANCH_NAME, "enable_e2e_regression").size() > 0) {
      env.E2E_SUITE = "regression"
      enableE2eTest()
    }
  }

  onMaster() {
    enablePactAs([
      AppPipelineDsl.PactRoles.CONSUMER,
      AppPipelineDsl.PactRoles.CONSUMER_DEPLOY_CHECK
    ])
    enableHighLevelDataSetup("", true)
    setAatEnvVars()
    enableE2eTest()
  }

  onDemo {
    setDemoEnvVars()
  }

  onIthc {
    setIthcEnvVars()
  }

  onPerftest {
    setPerftestEnvVars()
    enableHighLevelDataSetup()
  }

  before('highleveldatasetup') {
    onPR{
      env.DEFINITION_STORE_URL_BASE = "https://ccd-definition-store-pcs-api-pr-${CHANGE_ID}.preview.platform.hmcts.net"
      env.CASE_API_URL = "https://pcs-api-pr-${CHANGE_ID}.preview.platform.hmcts.net"
      env.DATA_STORE_URL_BASE="https://ccd-data-store-api-pcs-api-pr-${CHANGE_ID}.preview.platform.hmcts.net"
    }
    onMaster{
      env.DEFINITION_STORE_URL_BASE = "http://ccd-definition-store-api-aat.service.core-compute-aat.internal"
      env.DATA_STORE_URL_BASE="http://ccd-data-store-api-aat.service.core-compute-aat.internal"
      env.CASE_API_URL = "https://pcs-api-staging.aat.platform.hmcts.net"
      env.CASE_TYPE_SUFFIX="staging"
      generateDefinitions(builder)
      env.CASE_API_URL = "http://pcs-api-aat.service.core-compute-aat.internal"
      env.CASE_TYPE_SUFFIX=""
    }
    onPerftest{
      env.DEFINITION_STORE_URL_BASE = "http://ccd-definition-store-api-perftest.service.core-compute-perftest.internal"
      env.CASE_API_URL = "http://pcs-api-perftest.service.core-compute-perftest.internal"
      env.DATA_STORE_URL_BASE="http://ccd-data-store-api-perftest.service.core-compute-perftest.internal"
    }
    generateDefinitions(builder)
  }

  afterAlways('functionalTest:preview') {
    archiveFunctionalTestReports()
    echo "[E2E Slack] functionalTest:preview afterAlways ran. CHANGE_ID=${env.CHANGE_ID}"
    if (env.CHANGE_ID) {
      def e2eRan = sh(script: 'test -f e2e-output/widgets/summary.json || test -d src/e2eTest/allure-results', returnStatus: true) == 0
      echo "[E2E Slack] E2E artifacts present: ${e2eRan}"
      if (e2eRan) {
        archiveE2eTestReports()
        postAllureResultsToSlack('preview')
      }
    }
  }

  afterAlways('functionalTest:aat') {
    archiveFunctionalTestReports()
  }

  afterAlways('e2eTest:aat') {
    archiveE2eTestReports()
    if (currentBuild.currentResult in ['FAILURE', 'UNSTABLE']) {
      postAllureResultsToSlack('master')
    }
  }

  afterAlways('e2eTest:preview') {
    echo "[E2E Slack] e2eTest:preview afterAlways ran"
    archiveE2eTestReports()
    postAllureResultsToSlack('preview')
  }
  afterAlways('e2eTest:aks:preview') {
    echo "[E2E Slack] e2eTest:aks:preview afterAlways ran"
    archiveE2eTestReports()
    postAllureResultsToSlack('preview')
  }
  afterAlways('E2E Test - AKS preview') {
    echo "[E2E Slack] E2E Test - AKS preview afterAlways ran"
    archiveE2eTestReports()
    postAllureResultsToSlack('preview')
  }
  afterAlways('e2eTest') {
    echo "[E2E Slack] e2eTest afterAlways ran. CHANGE_ID=${env.CHANGE_ID}"
    if (env.CHANGE_ID != null && env.CHANGE_ID != '') {
      archiveE2eTestReports()
      postAllureResultsToSlack('preview')
    }
  }
}

def setPreviewEnvVars() {
  env.MANAGE_CASE_BASE_URL = "https://xui-pcs-api-pr-${CHANGE_ID}.preview.platform.hmcts.net"
  env.ENVIRONMENT= "preview"
  env.E2E_SUITE= "pr"
}

def setAatEnvVars() {
  env.MANAGE_CASE_BASE_URL = "https://manage-case.aat.platform.hmcts.net"
  env.ENVIRONMENT= "aat"
  env.E2E_SUITE= "regression"
}

def setDemoEnvVars() {
  env.ENVIRONMENT= "demo"
}

def setIthcEnvVars() {
  env.ENVIRONMENT= "ithc"
}

def setPerftestEnvVars() {
  env.ENVIRONMENT= "perftest"
}

def generateDefinitions(builder) {
  builder.gradle('--rerun-tasks generateCCDConfig')
  sh "./bin/create-xlsx.sh ${env.ENVIRONMENT}"
  steps.archiveArtifacts artifacts: "build/definitions/*.xlsx"
}

def archiveFunctionalTestReports() {
  publishHTML([
    allowMissing: true,
    alwaysLinkToLastBuild: true,
    keepAll: true,
    reportDir: "report-for-functional-tests",
    reportFiles: 'index.html',
    reportName: 'API Functional Test Report'
  ])

  archiveArtifacts allowEmptyArchive: true, artifacts: '**/report-for-functional-tests/*'
}

def archiveE2eTestReports() {
  publishHTML([
    allowMissing: true,
    alwaysLinkToLastBuild: true,
    keepAll: true,
    reportDir: "e2e-output",
    reportFiles: 'index.html',
    reportName: 'E2E Test Report'
  ])

  archiveArtifacts allowEmptyArchive: true, artifacts: '**/e2e-output/**/*'
}

// E2E Slack notification (master: only on failure; preview: when E2E runs).
def postAllureResultsToSlack(String pipelineType = 'master') {
  echo "[E2E Slack] postAllureResultsToSlack called pipelineType=${pipelineType}"
  env.E2E_SERVICE_NAME = 'pcs-api'
  env.E2E_PIPELINE_TYPE = pipelineType
  def msg = sh(
    script: "cd src/e2eTest && E2E_SERVICE_NAME=pcs-api E2E_PIPELINE_TYPE=${pipelineType} ./node_modules/.bin/tsx ./scripts/allure-slack-notifier.ts --print-only || true",
    returnStdout: true
  ).trim()
  if (!msg) {
    msg = "E2E Test Results — Build #${env.BUILD_NUMBER}\n*Service:* pcs-api  |  *Pipeline:* ${pipelineType}\n\nAllure report not available – check build logs.\n*Allure report:* ${env.BUILD_URL}E2E_20Test_20Report/"
  }
  try {
    slackSend(channel: '#hdp-qa-e2e-test-results', message: msg)
    echo "[E2E Slack] Notification sent to #hdp-qa-e2e-test-results"
  } catch (Exception e) {
    echo "WARNING: Failed to send E2E Slack notification: ${e.message}. Check Jenkins Slack plugin and channel access."
  }
}



