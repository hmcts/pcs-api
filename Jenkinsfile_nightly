#!groovy

properties([
  // H allow predefined but random minute see https://en.wikipedia.org/wiki/Cron#Non-standard_characters
  pipelineTriggers([cron('H 07 * * 1-5')]),
  parameters([
    choice(
      name: 'ENVIRONMENT',
      choices: ['aat', 'preview'],
      description: 'Environment to run the test.'
    ),
    string(
      name: 'PR_NUMBER',
      defaultValue: '',
      description: 'PR number (REQUIRED if environment is preview, leave blank otherwise).'
    ),
  ])
])

@Library("Infrastructure")

def type = "java"
def product = "pcs"
def component = "api"

static Map<String, Object> secret(String secretName, String envVariable) {
  [$class     : 'AzureKeyVaultSecret',
    secretType : 'Secret',
    name       : secretName,
    envVariable: envVariable
  ]
}

def secrets = [
  'pcs-${env}': [
    secret('pcs-api-idam-secret', 'PCS_API_IDAM_SECRET'),
    secret('idam-system-user-name', 'IDAM_SYSTEM_USERNAME'),
    secret('idam-system-user-password', 'IDAM_SYSTEM_USER_PASSWORD'),
    secret('idam-pcs-user-password', 'IDAM_PCS_USER_PASSWORD'),
    secret('pcs-solicitor-automation-uid', 'PCS_SOLICITOR_AUTOMATION_UID'),
    secret('pcs-prd-admin-username', 'PCS_PRD_ADMIN_USERNAME'),
    secret('pcs-prd-admin-password', 'PCS_PRD_ADMIN_PASSWORD')
  ]
]

withNightlyPipeline(type, product, component) {
  validateInputs()
  enableSlackNotifications('#pcs-tech')
  loadVaultSecrets(secrets)
  handleEnvironmentSetting()
  enableFortifyScan()

  enableE2eTest()

  afterAlways('fortify-scan') {
    steps.archiveArtifacts allowEmptyArchive: true, artifacts: '**/Fortify Scan/**/*'
  }

  afterAlways('E2eTest') {
    postAllureResultsToSlack()
  }
}

def validateInputs() {
  if (params.ENVIRONMENT == 'preview' && (!params.PR_NUMBER?.trim())) {
    error("Validation Failed: PR number is required when environment is 'preview'")
  }
  if (params.ENVIRONMENT == 'preview' && !params.PR_NUMBER.trim().matches("^\\d+\$")) {
    error("Validation Failed: PR number must contain only numerical values")
  }
}

def handleEnvironmentSetting() {
  env.MANAGE_CASE_BASE_URL = (params.ENVIRONMENT == 'preview')
    ? "https://xui-pcs-api-pr-${params.PR_NUMBER}.preview.platform.hmcts.net"
    : "https://manage-case.${params.ENVIRONMENT}.platform.hmcts.net"

  env.DATA_STORE_URL_BASE = (params.ENVIRONMENT == 'preview')
    ? "https://ccd-data-store-api-pcs-api-pr-${params.PR_NUMBER}.preview.platform.hmcts.net"
    : "http://ccd-data-store-api-${params.ENVIRONMENT}.service.core-compute-${params.ENVIRONMENT}.internal"

  env.E2E_SUITE = (params.ENVIRONMENT == 'preview')
    ? "pr"
    : "pr" //Revert to chrome after testing
}

// E2E stage notification to #hdp-qa-e2e-test-results
def postAllureResultsToSlack() {
  env.E2E_SERVICE_NAME = 'pcs-api'
  env.E2E_PIPELINE_TYPE = 'nightly'
  def msg = sh(
    script: "cd src/e2eTest && E2E_SERVICE_NAME=pcs-api E2E_PIPELINE_TYPE=nightly ./node_modules/.bin/tsx ./scripts/allure-slack-notifier.ts --print-only || true",
    returnStdout: true
  ).trim()
  if (!msg) {
    msg = "E2E Test Results — Build #${env.BUILD_NUMBER}\n*Service:* pcs-api  |  *Pipeline:* nightly\n\nAllure report not available – check build logs.\n*Allure report:* ${env.BUILD_URL}E2E_20Test_20Report/"
  }
  try {
    slackSend(channel: '#hdp-qa-e2e-test-results', message: msg)
    echo "E2E Slack notification sent to #hdp-qa-e2e-test-results"
  } catch (Exception e) {
    echo "WARNING: Failed to send E2E Slack notification: ${e.message}. Check Jenkins Slack plugin and channel access."
  }
}
